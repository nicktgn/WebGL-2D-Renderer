<!DOCTYPE html>
<html>
<head>
   <title></title>
</head>
<body>
   <canvas id="canvas" width="1024" height="640"></canvas>

   <div id="uiContainer">
     <div id="ui">
       <div id="x"></div>
       <div id="y"></div>
       <div id="angle"></div>
       <div id="scaleX"></div>
       <div id="scaleY"></div>
     </div>
   </div>

   <script id="2d-vertex-shader" type="notjs">
attribute vec2 a_position;

uniform mat3 u_model;
uniform mat3 u_view;
uniform mat3 u_projection;

void main() {
   mat3 pvm = u_projection * u_view * u_model;
   gl_Position = vec4((pvm * vec3(a_position, 1)).xy, 0, 1);
}
   </script>
   <script id="2d-fragment-shader" type="notjs">
precision mediump float;

uniform vec4 u_color;

void main() {
   gl_FragColor = u_color;
}
   </script>

<!-------------------------------------->
   <script id="stroke-vertex-shader" type="notjs">
attribute vec2 a_position;
attribute float a_direction; 
attribute vec2 a_next;
attribute vec2 a_previous;

uniform mat3 u_model;
uniform mat3 u_view;
uniform mat3 u_projection;

uniform float u_aspect;
uniform float u_strokeWidth;
uniform int u_strokeMiter;
uniform int u_strokePosition;

void main() {
   vec2 aspectVec = vec2(u_aspect, 1.0);

   vec2 currentWorld = (u_model * vec3(a_position, 1)).xy;
   vec2 previousWorld = (u_model * vec3(a_previous, 1)).xy;
   vec2 nextWorld = (u_model * vec3(a_next, 1)).xy;

   float len = u_strokeWidth;
   float orientation = a_direction;
   float d = 1.0;

   //starting point uses (next - current)
   vec2 dir = vec2(0.0);
   if (currentWorld == previousWorld) {
      dir = normalize(nextWorld - currentWorld);
   }
   //ending point uses (current - previous)
   else if (currentWorld == nextWorld) {
      dir = normalize(currentWorld - previousWorld);
   }
   //somewhere in middle, needs a join
   else{
      //get directions from (C - B) and (B - A)
      vec2 dirA = normalize((currentWorld - previousWorld));
      if (u_strokeMiter == 1) {
         vec2 dirB = normalize((nextWorld - currentWorld));
         //now compute the miter join normal and length
         vec2 tangent = normalize(dirA + dirB);
         vec2 perp = vec2(-dirA.y, dirA.x);
         vec2 miter = vec2(-tangent.y, tangent.x);
         dir = tangent;
         len = u_strokeWidth / dot(miter, perp);
         //d = dot(miter, perp);
      } else {
         dir = dirA;
      }
   }
  
   vec2 normal = vec2(-dir.y, dir.x);

   float factor;
   // center
   if (u_strokePosition == 0){
      factor = len/2.0 * orientation;
   }
   // outside
   else if(u_strokePosition > 0){
      factor = min(0.0, float(len * orientation));
   }
   // inside
   else{
      factor = max(0.0, float(len * orientation));
   } 

   vec2 offset = normal * factor;
   vec2 shifted = currentWorld + offset;

   mat3 pv = u_projection * u_view;

   gl_Position = vec4((pv * vec3(shifted, 1)).xy, 0, 1);
   gl_PointSize = 1.0;
}                                                              
   </script>

   <!--
   for most samples webgl-utils only provides shader compiling/linking and
   canvas resizing because why clutter the examples with code that's the same in every sample.
   See http://webglfundamentals.org/webgl/lessons/webgl-boilerplate.html
   and http://webglfundamentals.org/webgl/lessons/webgl-resizing-the-canvas.html
   for webgl-utils, m3, m4, and webgl-lessons-ui.
   -->

   <!--
   <script type="text/javascript" src="https://webglfundamentals.org/webgl/resources/webgl-lessons-ui.js"></script>
   <script type="text/javascript" src="https://webglfundamentals.org/webgl/resources/webgl-utils.js"></script>
   -->
   <script type="module" src="main.js"></script>
</body>
</html>